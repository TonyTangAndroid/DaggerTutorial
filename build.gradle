buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.0'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
        classpath 'com.google.dagger:hilt-android-gradle-plugin:2.28-alpha'
    }
}

plugins {
    id "com.github.sherter.google-java-format" version "0.8"
}

allprojects {
    repositories {
        google()
        jcenter()
        maven {
            url "https://androidx.dev/snapshots/builds/6543454/artifacts/repository/"
        }
    }
}

subprojects { project ->
    project.tasks.withType(JavaCompile) {
        dependsOn(installGitHooks)
    }

    repositories {
        jcenter()
        google()
    }

}

googleJavaFormat {
    toolVersion = "1.7"
}

////////////////////////////////////////////////////////////////////////
//
//  Google Java Format pre-commit hook installation
////////////////////////////////////////////////////////////////////////

tasks.register('installGitHooks', Copy) {
    from(file('config/hooks/pre-commit-stub')) {
        rename 'pre-commit-stub', 'pre-commit'
    }
    into file('.git/hooks')
    fileMode 0777
}